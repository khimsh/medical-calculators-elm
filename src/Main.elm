port module Main exposing (main)

import Browser
import Browser.Navigation as Nav
import Calculators.FreeWaterDeficit as FreeWaterDeficit
import Calculators.Liquids as Liquids
import Calculators.Nutrition as Nutrition
import Calculators.Pills as Pills
import Html exposing (button, div, h1, h2, header, main_, p, text)
import Html.Attributes exposing (attribute, class, type_)
import Html.Events exposing (onClick)
import Translations exposing (Language(..), getStrings)
import Url
import Url.Parser exposing (..)


port setLangAttribute : String -> Cmd msg


main : Program () Model Msg
main =
    Browser.application
        { init = init
        , update = update
        , view = view
        , subscriptions = \_ -> Sub.none
        , onUrlChange = UrlChanged
        , onUrlRequest = LinkClicked
        }


type Calculator
    = PillsCalc
    | LiquidsCalc
    | NutritionCalc
    | FreeWaterDeficitMsg


type View
    = IndexView
    | CalculatorView Calculator


type alias Model =
    { currentView : View
    , language : Language
    , pills : Pills.Model
    , liquids : Liquids.Model
    , nutrition : Nutrition.Model
    , freeWaterDeficit : FreeWaterDeficit.Model
    , sidebarOpen : Bool
    , navKey : Nav.Key
    }


init : () -> Url.Url -> Nav.Key -> ( Model, Cmd Msg )
init _ url navKey =
    let
        initialView =
            parseUrl url
    in
    ( { currentView = initialView
      , language = Georgian
      , pills = Pills.init
      , liquids = Liquids.init
      , nutrition = Nutrition.init
      , freeWaterDeficit = FreeWaterDeficit.init
      , sidebarOpen = False
      , navKey = navKey
      }
    , Cmd.none
    )


type Msg
    = SelectCalculator Calculator
    | GoToIndex
    | ToggleSidebar
    | ToggleLanguage
    | PillsMsg Pills.Msg
    | LiquidsMsg Liquids.Msg
    | NutritionMsg Nutrition.Msg
    | HandleFreeWaterDeficitMsg FreeWaterDeficit.Msg
    | LinkClicked Browser.UrlRequest
    | UrlChanged Url.Url


parseUrl : Url.Url -> View
parseUrl url =
    case url.fragment of
        Just "pills" ->
            CalculatorView PillsCalc

        Just "liquids" ->
            CalculatorView LiquidsCalc

        Just "nutrition" ->
            CalculatorView NutritionCalc

        Just "free-water-deficit" ->
            CalculatorView FreeWaterDeficitMsg

        _ ->
            IndexView


calculatorToFragment : Calculator -> String
calculatorToFragment calc =
    case calc of
        PillsCalc ->
            "pills"

        LiquidsCalc ->
            "liquids"

        NutritionCalc ->
            "nutrition"

        FreeWaterDeficitMsg ->
            "free-water-deficit"


update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        SelectCalculator calculator ->
            ( { model | currentView = CalculatorView calculator, sidebarOpen = False }
            , Nav.pushUrl model.navKey ("#" ++ calculatorToFragment calculator)
            )

        GoToIndex ->
            ( { model | currentView = IndexView, sidebarOpen = False }
            , Nav.pushUrl model.navKey "./"
            )

        ToggleLanguage ->
            let
                newLanguage =
                    if model.language == English then
                        Georgian

                    else
                        English

                langAttribute =
                    case newLanguage of
                        English ->
                            "en"

                        Georgian ->
                            "ka"
            in
            ( { model | language = newLanguage }
            , setLangAttribute langAttribute
            )

        ToggleSidebar ->
            ( { model | sidebarOpen = not model.sidebarOpen }
            , Cmd.none
            )

        PillsMsg subMsg ->
            let
                strings =
                    getStrings model.language
            in
            ( { model | pills = Pills.update subMsg model.pills strings }
            , Cmd.none
            )

        LiquidsMsg subMsg ->
            let
                strings =
                    getStrings model.language
            in
            ( { model | liquids = Liquids.update subMsg model.liquids strings }
            , Cmd.none
            )

        NutritionMsg subMsg ->
            let
                strings =
                    getStrings model.language
            in
            ( { model | nutrition = Nutrition.update subMsg model.nutrition strings }
            , Cmd.none
            )

        HandleFreeWaterDeficitMsg subMsg ->
            let
                updatedFreeWaterDeficit =
                    FreeWaterDeficit.update subMsg model.freeWaterDeficit
            in
            ( { model | freeWaterDeficit = updatedFreeWaterDeficit }, Cmd.none )

        LinkClicked urlRequest ->
            case urlRequest of
                Browser.Internal url ->
                    ( model, Nav.pushUrl model.navKey (Url.toString url) )

                Browser.External href ->
                    ( model, Nav.load href )

        UrlChanged url ->
            ( { model | currentView = parseUrl url }
            , Cmd.none
            )


view : Model -> Browser.Document Msg
view model =
    let
        strings =
            getStrings model.language

        sidebarClass =
            if model.sidebarOpen then
                "sidebar-nav open"

            else
                "sidebar-nav"
    in
    { title = strings.title
    , body =
        [ div [ class "page-wrapper" ]
            [ button
                [ class "sidebar-toggle-button"
                , type_ "button"
                , onClick ToggleSidebar
                , attribute "aria-label" "Toggle menu"
                ]
                [ text "‚ò∞" ]
            , div [ class sidebarClass ]
                [ button
                    [ class "sidebar-nav-item"
                    , type_ "button"
                    , onClick ToggleLanguage
                    , attribute "aria-label"
                        (if model.language == English then
                            "Switch to Georgian"

                         else
                            "Switch to English"
                        )
                    , attribute "title"
                        (if model.language == English then
                            "·É•·Éê·É†·Éó·É£·Éö·Éò"

                         else
                            "English"
                        )
                    ]
                    [ text
                        (if model.language == English then
                            "üá¨üá™"

                         else
                            "üá¨üáß"
                        )
                    ]
                , button
                    [ class
                        ("sidebar-nav-item"
                            ++ (if model.currentView == CalculatorView PillsCalc then
                                    " active"

                                else
                                    ""
                               )
                        )
                    , type_ "button"
                    , onClick (SelectCalculator PillsCalc)
                    , attribute "aria-label" "Pills"
                    , attribute "title" strings.pillDosage
                    ]
                    [ text "üíä"
                    , Html.span [ class "sidebar-nav-item-text" ] [ text strings.pillDosage ]
                    ]
                , button
                    [ class
                        ("sidebar-nav-item"
                            ++ (if model.currentView == CalculatorView LiquidsCalc then
                                    " active"

                                else
                                    ""
                               )
                        )
                    , type_ "button"
                    , onClick (SelectCalculator LiquidsCalc)
                    , attribute "aria-label" "Liquids"
                    , attribute "title" strings.liquidDosage
                    ]
                    [ text "üß™"
                    , Html.span [ class "sidebar-nav-item-text" ] [ text strings.liquidDosage ]
                    ]
                , button
                    [ class
                        ("sidebar-nav-item"
                            ++ (if model.currentView == CalculatorView NutritionCalc then
                                    " active"

                                else
                                    ""
                               )
                        )
                    , type_ "button"
                    , onClick (SelectCalculator NutritionCalc)
                    , attribute "aria-label" "Nutrition"
                    , attribute "title" strings.nutrition
                    ]
                    [ text "ü•ó"
                    , Html.span [ class "sidebar-nav-item-text" ] [ text strings.nutrition ]
                    ]
                , button
                    [ class
                        ("sidebar-nav-item"
                            ++ (if model.currentView == CalculatorView FreeWaterDeficitMsg then
                                    " active"

                                else
                                    ""
                               )
                        )
                    , type_ "button"
                    , onClick (SelectCalculator FreeWaterDeficitMsg)
                    , attribute "aria-label" "Free Water Deficit"
                    , attribute "title" strings.freeWaterDeficit
                    ]
                    [ text "üíß"
                    , Html.span [ class "sidebar-nav-item-text" ] [ text strings.freeWaterDeficit ]
                    ]
                ]
            , div [ class "main-wrapper" ]
                [ div [ class "disclaimer-banner" ] [ text strings.disclaimer ]
                , header [ class "header", attribute "aria-label" "Site header" ]
                    [ div [ class "header-left" ]
                        [ h1 [ class "title" ] [ text strings.title ]
                        ]
                    ]
                , main_ [ class "main-content" ]
                    [ case model.currentView of
                        IndexView ->
                            viewIndex strings

                        CalculatorView calculator ->
                            viewCalculator model calculator strings
                    ]
                ]
            ]
        ]
    }


viewIndex : Translations.Strings -> Html.Html Msg
viewIndex strings =
    div [ class "index-container" ]
        [ div [ class "calculators-grid" ]
            [ calculatorCard PillsCalc strings.pillDosage strings.pillDescription "#ee5a52"
            , calculatorCard LiquidsCalc strings.liquidDosage strings.liquidDescription "#3498db"
            , calculatorCard NutritionCalc strings.nutrition strings.nutritionDescription "#2ecc71"
            , calculatorCard FreeWaterDeficitMsg strings.freeWaterDeficit strings.freeWaterDeficitDescription "#f39c12"
            ]
        ]


calculatorCard : Calculator -> String -> String -> String -> Html.Html Msg
calculatorCard calculator title description color =
    button
        [ class "calculator-card-button"
        , onClick (SelectCalculator calculator)
        , attribute "style" ("--accent-color: " ++ color)
        ]
        [ div [ class "card-header" ]
            [ div [ class "card-blob" ] [] ]
        , div [ class "card-content" ]
            [ h2 [ class "card-title" ] [ text title ]
            , p [ class "card-description" ] [ text description ]
            ]
        ]


viewCalculator : Model -> Calculator -> Translations.Strings -> Html.Html Msg
viewCalculator model calculator strings =
    div [ class "calculator-wrapper" ]
        [ button
            [ class "back-button"
            , type_ "button"
            , onClick GoToIndex
            , attribute "aria-label" "Back to index"
            ]
            [ text "‚Üê Back" ]
        , if calculator == PillsCalc then
            Html.map PillsMsg (Pills.view strings model.pills)

          else if calculator == LiquidsCalc then
            Html.map LiquidsMsg (Liquids.view strings model.liquids)

          else if calculator == NutritionCalc then
            Html.map NutritionMsg (Nutrition.view strings model.nutrition)

          else
            Html.map HandleFreeWaterDeficitMsg (FreeWaterDeficit.view strings model.freeWaterDeficit)
        ]
